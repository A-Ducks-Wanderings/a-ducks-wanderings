{
	id: "charles",
	prototype: ["player_controlled"],
	friction: 2000,
	editor_info: {
		category: "charles"
	},

	properties: {
		jump_speed: -100,
		walk_speed: 150,
		fall_speed: 80,
		jump_walk_speed: 50,
		crawl_speed: 130,
		last_cycle_standing: { type: "int", default: -10000, persistent: false },
		coyote_time: 10
	},

/* --- handle animations --- */

	on_end_stand_anim: "[
		set(animation, 'stand') // loop the stand animation
	]",

	on_end_attack_anim: "[
		set(animation, 'stand') // set the animation to stand after attacking
	]",

	on_end_crouch_anim: "[
		set(animation, 'exit_crouch')
	]",

	on_end_walk_anim: "[
		set(animation, 'walk') // loop the walk animation
	]",

	on_end_jump_anim: "[
		set(animation, 'fall')
	]",

	on_end_enter_crouch_anim: "[
		set(animation, 'crouch')
	]",

	on_end_exit_crouch_anim: "[
		set(animation, 'stand')
	]",

/* --- handle key presses --- */

	on_ctrl_tongue: "[
		if(animation in ['stand', 'walk'],
		[
			set(animation, 'attack')
		]
		)
		]",

	on_ctrl_down: "[
		if(animation in ['stand', 'walk'],[
				set(animation, 'enter_crouch')
			]
		)
	]",

	on_ctrl_jump: "[
		if(animation in ['stand', 'walk'] and (cycle - last_cycle_standing) < coyote_time,
			[
				set(animation, 'jump') // jump
			]
		)
	]",

/* --- processes --- */

	on_process: "[
			if((ctrl_left or ctrl_right) and animation in ['stand'],
				[
					set(animation, 'walk') // walk
				]
			),
			if(animation in ['jump', 'fall'],
				[
					if(ctrl_left, [
						set(facing, -1),
						add(velocity_x, -jump_walk_speed)
					]),
					if(ctrl_right, [
						set(facing, 1),
						add(velocity_x, jump_walk_speed)
					])
				]
			),
			if(is_standing, set(last_cycle_standing, cycle))
		]",

	on_process_crouch: "[
		if(time_in_animation = 23 and ctrl_down, 
			[
				set(time_in_animation, 0)
			]
		),
		if(not ctrl_down, [
			set(animation, 'exit_crouch')
		]),
		if((ctrl_left or ctrl_right),
				[
					set(facing, if(ctrl_left, -1, 1)),
					add(velocity_x, if(ctrl_left, -crawl_speed, crawl_speed))
				]
		)
	]",

	on_process_attack: "[
		if((ctrl_left or ctrl_right),
				[
					set(facing, if(ctrl_left, -1, 1)),
					add(velocity_x, if(ctrl_left, -walk_speed, walk_speed))
				]
		),
	]",

	on_process_walk: "[
		if(not (ctrl_left or ctrl_right),
		[
			set(animation, 'stand') // stop walking if keys not pressed
		],
		[
			set(facing, if(ctrl_left, -1, 1)), // make sure we're facing the right direction
			add(velocity_x, if(ctrl_left, -walk_speed, walk_speed))
		]
		)
	]",

	on_process_jump: "[
		if(ctrl_jump, [
			add(velocity_y, jump_speed)
		]
		)
	]",

	on_process_fall: "[
		add(velocity_y, fall_speed)
	]",

/* --- other events --- */

	on_collide_feet: "[
		set(animation, 'stand') // on feet collision, stand
	]",

	on_change_animation_failure: "[
		set(animation, arg.previous_animation)
	]",

	animation: [
		{
			"@base": true,
			image: "characters/charles-spritesheet1.png",
			pad: 3,
			solid_area: [10, 4, 21, 30]
		},
		{
			id: "stand",
			rect: [2, 2, 33, 33],
			duration: 6,
			frames: 4,
			accel_y: 80
		},
		{
			id: "enter_crouch",
			rect: [2, 177, 33, 208],
			duration: 1.2,
			frames: 5,
			accel_y: 80
		},
		{
			id: "crouch",
			rect: [2, 212, 33, 243],
			solid_area: [10, 16, 21, 30],
			duration: 12,
			frames: 2,
			accel_y: 80
		},
		{
			id: "exit_crouch",
			rect: [37, 177, 68, 208],
			duration: 1,
			frames: 2,
			accel_y: 80,
			reverse: true
		},
		{
			id: "fall",
			rect: [2, 107, 33, 138],
			duration: 6,
			frames: 2,
			accel_y: 80
		},
		{
			id: "glide",
			rect: [2, 247, 33, 278],
			duration: 6,
			frames: 3,
			accel_y: 80,
			reverse: true
		},
		{
			id: "jump",
			rect: [2, 142, 33, 173],
			duration: 6,
			frames: 2,
			accel_y: 80,
			velocity_y: -1200
		},
		{
			id: "walk",
			rect: [2, 72, 33, 103],
			duration: 4,
			frames: 4,
			accel_y: 80
		},
		{
			id: "attack",
			rect: [2, 37, 33, 68],
			solid_area: [10, 4, 21, 30],
			duration: 4,
			frames: 5,
			accel_y: 80
		},
	]
}