{
	id: "charles",
	prototype: ["player_controlled"],
	friction: 2000,
	editor_info: {
		category: "charles"
	},

	properties: {
		// on-ground x speeds
		walk_speed: 120,
		run_speed: 180,
		crawl_speed: 110,

		// in-air x speeds
		air_walk_speed: 40,
		air_run_speed: 50,
		air_glide_walk_speed: 80,
		air_glide_run_speed: 100,

		// in-air y speeds
		walking_jump_power: -125,
		running_jump_power: -170,
		air_glide_fall_speed: -20,
		fall_speed: 80,
		glide_fall_speed: 40,
		
		// speed caps
		running_speed_cap: 1000,
		walking_speed_cap: 400,
		gliding_speed_cap: 400

		// other
		last_cycle_standing: { type: "int", default: -10000, persistent: false },
		coyote_time: 15,
		ctrl_run: { type: "bool", default: false, persistent: false },
		
	},

/* --- handle animations --- */

	on_end_stand_anim: "[
		set(animation, 'stand') // loop the stand animation
	]",

	on_end_attack_anim: "[
		set(animation, 'stand') // set the animation to stand after attacking
	]",

	on_end_crouch_anim: "[
		set(animation, 'exit_crouch') // when we're done with crouching, we exit crouching
	]",

	on_end_crawl_anim: "[
		set(animation, 'crawl') // loop the crawl animation
	]",

	on_end_walk_anim: "[
		set(animation, 'walk') // loop the walk animation
	]",

	on_end_run_anim: "[
		set(animation, 'run') // loop the run animation
	]",

	on_end_jump_anim: "[
		set(animation, 'fall') // once we're done with our jump animation, we fall
	]",

	on_end_glide_anim:  "[
		set(animation, 'glide') // loop the glide animation
	]",
	
	on_end_fall_anim: "[
		set(animation, 'fall') // loop the fall animation
	]",

	on_end_enter_crouch_anim: "[
		set(animation, 'crouch') // enter crouching
	]",

	on_end_exit_crouch_anim: "[
		set(animation, 'stand') // exit crouching
	]",

/* --- handle key presses --- */

	on_ctrl_tongue: "[
		if(animation in ['stand', 'walk'],
		[
			set(animation, 'attack') // attack if we're standing or walking
		]
		)
		]",

	on_ctrl_down: "[
		if(animation in ['stand', 'walk'],[
				set(animation, 'enter_crouch') // if we're standing or walking, we crouch
			]
		)
	]",

	on_ctrl_jump: "[
		if(animation in ['stand', 'walk', 'run'] and (cycle - last_cycle_standing) < coyote_time,
			[
				set(animation, 'jump') // if we're walking or standing and we're within our coyote time, we jump
			]
		),
		if(animation in ['fall'], [
			set(animation, 'glide') // if we press jump while falling, we glide
		])
	]",

/* --- processes --- */

	on_process: "[
			if((ctrl_left or ctrl_right) and animation in ['stand'],
				[
					set(animation, 'walk') // only walk if we're standing
				]
			),
			if(is_standing, [
				set(last_cycle_standing, cycle) // set our coyote time if standing
			]),
			set(ctrl_run, ('d' in ctrl_keys)), // set run controls
			debug(velocity_x)
		]",

	on_process_crouch: "[
		if(time_in_animation = 23 and ctrl_down, 
			[
				set(time_in_animation, 0)
			]
		),
		if(not ctrl_down, [
			set(animation, 'exit_crouch')
		]),
		if(ctrl_left or ctrl_right,
			[
				set(animation, 'crawl')
			]
		),
		if(not is_standing, [
			set(animation, 'fall')
		])
	]",

	on_process_attack: "[
		if((ctrl_left or ctrl_right),
				[
					set(facing, if(ctrl_left, -1, 1)),
					add(velocity_x, if(ctrl_left, -walk_speed, walk_speed))
				]
		),
	]",

	on_process_walk: "[
		if(not (ctrl_left or ctrl_right), [
			set(animation, 'stand') // stop walking if keys not pressed
		],
		[
			set(facing, if(ctrl_left, -1, 1)), // make sure we're facing the right direction
			if(is_standing, [
				add(velocity_x, if(ctrl_left, -walk_speed, walk_speed))
			], [
				add(velocity_x, if(ctrl_left, -air_walk_speed, air_walk_speed))
			])
		]),
		if(ctrl_run, [
			set(animation, 'run') // if run key pressed, run
		]),
		if(abs(velocity_x) > walking_speed_cap, [
			set(velocity_x, if(velocity_x < 0, -walking_speed_cap, walking_speed_cap))
		])
	]",
	on_process_run: "[
		if(not ctrl_run, [
			set(animation, 'walk') // if not run key pressed, walk.
		]),
		if(not (ctrl_left or ctrl_right), [
			set(animation, 'stand') // stop running if keys not pressed
		],
		[
			set(facing, if(ctrl_left, -1, 1)), // make sure we're facing the right direction
			if(is_standing, [
				add(velocity_x, if(ctrl_left, -run_speed, run_speed))
			], [
				add(velocity_x, if(ctrl_left, -air_run_speed, air_run_speed))
			])
		]),
		if(abs(velocity_x) > running_speed_cap, [
			set(velocity_x, if(velocity_x < 0, -running_speed_cap, running_speed_cap))
		])
	]",

	on_process_jump: "[
		if(ctrl_jump, [
			if(ctrl_run and velocity_x > 600, [
				add(velocity_y, running_jump_power)
			], [
				add(velocity_y, walking_jump_power)
			])
		]),
		if(ctrl_left or ctrl_right, [
			if(ctrl_run, [
				add(velocity_x, if(ctrl_left, -air_run_speed, air_run_speed))
			], [
				add(velocity_x, if(ctrl_left, -air_walk_speed, air_walk_speed))
			]),
			set(facing, if(ctrl_left, -1, 1))
		])
	]",

	on_process_fall: "[
		add(velocity_y, fall_speed),
		if(ctrl_left or ctrl_right, [
			if(ctrl_run, [
				add(velocity_x, if(ctrl_left, -air_run_speed, air_run_speed))
			], [
				add(velocity_x, if(ctrl_left, -air_walk_speed, air_walk_speed))
			]),
			set(facing, if(ctrl_left, -1, 1))
		])
	]",

	on_process_glide: "[
		add(velocity_y, glide_fall_speed),
		if(ctrl_left or ctrl_right, [
			if(ctrl_run, [
				add(velocity_x, if(ctrl_left, -air_glide_run_speed, air_glide_run_speed))
			], [
				add(velocity_x, if(ctrl_left, -air_glide_walk_speed, air_glide_walk_speed))
			]),
			set(facing, if(ctrl_left, -1, 1))
		]),
		if(abs(velocity_x) > gliding_speed_cap, [
			set(velocity_x, if(velocity_x < 0, -gliding_speed_cap, gliding_speed_cap))
		])
	]",

	on_process_enter_crouch: "[
		if(ctrl_left or ctrl_right,
			[
				set(facing, if(ctrl_left, -1, 1)),
				add(velocity_x, if(ctrl_left, -crawl_speed, crawl_speed))
			]
		),
		if(not ctrl_down, [
			set(animation, 'exit_crouch')
		])
	]",

	on_process_crawl: "[
		if(ctrl_left or ctrl_right,
			[
				set(facing, if(ctrl_left, -1, 1)),
				if(is_standing, [
					add(velocity_x, if(ctrl_left, -crawl_speed, crawl_speed))
				], [
					add(velocity_x, if(ctrl_left, -fall_speed, fall_speed))
				])
				
			],
			[
				set(animation, 'crouch')
			]
		),
		if(not is_standing, [
			set(animation, 'fall')
		]),
		if(not ctrl_down, [
			set(animation, 'exit_crouch')
		]),
		
	]",

	on_process_exit_crouch: "[
		if(ctrl_left or ctrl_right,
			[
				set(facing, if(ctrl_left, -1, 1)),
				add(velocity_x, if(ctrl_left, -crawl_speed, crawl_speed))
			]
		),
	]",

/* --- other events --- */

	on_collide_feet: "[
		set(animation, 'stand') // on feet collision, stand
	]",

	on_change_animation_failure: "[
		set(animation, arg.previous_animation)
	]",

	animation: [
		{
			"@base": true,
			image: "characters/charles-spritesheet1.png",
			pad: 3,
			solid_area: [10, 4, 21, 30],
			accel_y: 80
		},
		{
			id: "stand",
			rect: [2, 2, 33, 33],
			duration: 12,
			frames: 2,
		},
		{
			id: "enter_crouch",
			rect: [2, 177, 33, 208],
			duration: 1.2,
			frames: 5,
		},
		{
			id: "crouch",
			rect: [2, 212, 33, 243],
			solid_area: [10, 16, 21, 30],
			duration: 12,
			frames: 2,
		},
		{
			id: "exit_crouch",
			rect: [37, 177, 68, 208],
			duration: 1,
			frames: 2,
			reverse: true
		},
		{
			id: "fall",
			rect: [2, 107, 33, 138],
			duration: 4,
			frames: 3,
			reverse: true
		},
		{
			id: "glide",
			rect: [2, 247, 33, 278],
			duration: 6,
			frames: 3,
			accel_y: 0,
			reverse: true
		},
		{
			id: "crawl",
			rect: [2, 282, 33, 313],
			duration: 4,
			frames: 4,
			reverse: true
		},
		{
			id: "jump",
			rect: [2, 142, 33, 173],
			duration: 6,
			frames: 2,
			velocity_y: -900
		},
		{
			id: "walk",
			rect: [2, 72, 33, 103],
			duration: 4,
			frames: 4,
		},
		{
			id: "run",
			rect: [2, 317, 33, 348],
			duration: 4,
			frames: 8,
		},
		{
			id: "attack",
			rect: [2, 37, 33, 68],
			solid_area: [10, 4, 21, 30],
			duration: 4,
			frames: 5,
		},
	]
}